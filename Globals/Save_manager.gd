extends Node

var save_name_1: String = "Slot 1"
var save_name_2: String = "Slot 2"
var save_name_3: String = "Autosave"

func save(savename: String):
	var save_state: SaveState = SaveState.new()
	save_state.ui_state = SceneManager.current_scene
	
	save_state.all_currencies = []
	for currency in CurrencyManager.all_currencies:
		save_state.all_currencies.append(currency.duplicate())
	
	save_state.all_jobs = []
	for job in JobManager.all_jobs:
		save_state.all_jobs.append(job.duplicate())
		
	save_state.mute = BackgroundMusicPlayer.stream_paused
	save_state.volume = BackgroundMusicPlayer.volume_linear
	
	ResourceSaver.save(save_state, "user://" + savename + ".tres")
	
	if savename != save_name_3:
		SignalHub.display.emit("Game saved: " + savename + "\n\n")
	
func load(savename: String):
	if not ResourceLoader.exists("user://" + savename + ".tres"):
		return
	var save_state: SaveState = ResourceLoader.load("user://" + savename + ".tres")
	
	SceneManager.set_scene(save_state.ui_state as SceneManager.Scene)
	
	for saved_currency in save_state.all_currencies:
		for currency in CurrencyManager.all_currencies:
			if saved_currency.name == currency.name:
				currency.amount = saved_currency.amount
				currency.has_been_seen = saved_currency.has_been_seen
				SignalHub.resource_updated.emit(currency, currency.amount)
				
	for saved_job in save_state.all_jobs:
		for job in JobManager.all_jobs:
			if saved_job.job_name == job.job_name:
				job.shows_up = saved_job.shows_up
				
	#BackgroundMusicPlayer.volume_db = save_state.volume
	#BackgroundMusicPlayer.stream_paused = save_state.mute
	
	SignalHub.volume_set.emit(save_state.volume, save_state.mute)
	
	for currency in CurrencyManager.all_currencies:
		currency.get_max()
		
	SignalHub.display.emit("Game loaded: " + savename + "\n\n")
	SceneManager.set_scene(SceneManager.Scene.CITY)

func _notification(what):
	match what:
		NOTIFICATION_APPLICATION_FOCUS_OUT:
			# window lost focus - keep running but maybe throttle fps
			Engine.max_fps = 10  # optional, saves CPU
		NOTIFICATION_APPLICATION_FOCUS_IN:
			Engine.max_fps = 60  # 0 = unlimited, back to normal
